!(defrec length (lambda (l) (if l (+ 1 (length (cdr l))) 0)))

;; Returns a pair: a boolean that is true if elt was removed from list; and the remaining elements in reverse order.
!(def maybe-remove (lambda (elt list)
                     (letrec ((aux (lambda (removed? acc elt list remaining)
                                     (if (> remaining 0)
                                         (if (eq elt (car list))
                                             (aux t
                                                  (if removed? (cons (car list) acc) acc)
                                                  elt
                                                  (if removed? list (cdr list))
                                                  (- remaining 1))
                                             (aux removed? (cons (car list) acc) elt (cdr list) (- remaining 1)))
                                         (cons removed? acc)))))
                       (aux nil () elt list (length list)))))

!(def score (lambda (code guess)
              (letrec ((aux (lambda (hits code-miss guess-miss code guess)
                              (if code
                                  (if (eq (car code) (car guess))
                                      (aux (+ 1 hits) code-miss guess-miss (cdr code) (cdr guess))
                                      (aux hits (cons (car code) code-miss) (cons (car guess) guess-miss) (cdr code) (cdr guess)))
                                  (letrec ((aux2 (lambda (partial-hits code-miss guess-miss)
                                                   (if code-miss
                                                       (let ((removed?-remaining (maybe-remove (car code-miss) guess-miss)))
                                                         (if (car removed?-remaining)
                                                             (aux2 (+ 1 partial-hits) (cdr code-miss) (cdr removed?-remaining))
                                                             (aux2 partial-hits (cdr code-miss) guess-miss)))
                                                       partial-hits))))
                                    (cons hits (aux2 0 code-miss guess-miss)))))))
                (aux 0 () () code guess))))

!(def code-valid? (lambda (code expected-length num-choices)
                    (if (= expected-length (length code))
                        (letrec ((aux (lambda (code)
                                        (if code
                                            (if (< (car code) num-choices)
                                                (if (>= (car code) 0)
                                                    (aux (cdr code))))
                                            t))))
                          (aux code)))))

!(def score-one-turn (lambda (code-commitment code-length num-choices guess)
                        (if (code-valid? (open code-commitment) code-length num-choices)
                            (if (code-valid? guess code-length num-choices)
                                (score (open code-commitment) guess)
                                :bad-guess)
                            :bad-code)))

;; Note that !(rand) produces strong secrets that cannot be brute-forced.
!(def code-comm-1 (hide !(rand) '(1 2 3 4)))
!(def code-comm-2 (hide !(rand) '(0 0 0 0)))
!(def code-comm-3 (hide !(rand) '(1 2 3 9)))
;; Tests to show that the implementation behaves as expected.
!(assert-eq '(1 . 2) (score-one-turn code-comm-1 4 6 '(0 2 4 3)))
!(assert-eq '(0 . 0) (score-one-turn code-comm-2 4 6 '(1 2 3 4)))
!(assert-eq :bad-code (score-one-turn code-comm-3 4 6 '(0 2 4 3)))
!(assert-eq :bad-guess (score-one-turn code-comm-1 4 6 '(9 2 4 3)))


;; Actually proving calls to the scoring function
!(def proof-key-1-2 !(prove (score-one-turn code-comm-1 4 6 '(0 2 4 3))))
!(def proof-key-0-0 !(prove (score-one-turn code-comm-2 4 6 '(1 2 3 4))))
!(def proof-key-bad-code !(prove (score-one-turn code-comm-3 4 6 '(0 2 4 3))))
!(def proof-key-bad-guess !(prove (score-one-turn code-comm-1 4 6 '(9 2 4 3))))

;; Verifying the proofs by their keys
!(assert !(verify proof-key-1-2))
!(assert !(verify proof-key-0-0))
!(assert !(verify proof-key-bad-code))
!(assert !(verify proof-key-bad-guess))
